buildscript {
    dependencies {
        classpath rootProject.ext.androidPlugin
        classpath rootProject.ext.psyncPlugin
        classpath rootProject.ext.versionsPlugin
        classpath rootProject.ext.aptPlugin
        classpath "com.fernandocejas.frodo:frodo-plugin:0.8.2"
        classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta4'
    }
}

//apply plugin: 'android-sdk-manager'   // Add back after https://github.com/JakeWharton/sdk-manager-plugin/issues/99
apply plugin: 'com.android.application'
apply plugin: 'com.flipboard.psync'
apply plugin: 'com.github.ben-manes.versions'   // ./gradlew dependencyUpdates -Drevision=release
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.fernandocejas.frodo'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "io.sweers.catchup"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.gitCommitCount
        versionName rootProject.ext.gitTag
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField 'String', 'GIT_SHA', /"${gitSha}"/
        buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp}"
        buildConfigField 'String', 'PROCUCT_HUNT_DEVELOPER_TOKEN', /"$product_hunt_developer_token"/
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file("placeholder")
            keyAlias "placeholder"
            storePassword "placeholder"
            keyPassword "placeholder"
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries = !rootProject.ext.ci
    }
    applicationVariants.all { variant ->
        // Delete unaligned APKs and manifest junk after
        def assembleTask = "assemble${variant.name.capitalize()}"
        tasks[assembleTask].doLast {
//            cleanOutputDir(file("build/outputs/apk/"))

            // Enable by adding "-P notify" to end of your ./gradlew command
            if (project.hasProperty("notify")) {
                "terminal-notifier -message '${assembleTask}-->DONE' -sound 'Ping'".execute()
            }
        }
    }
    lintOptions {
        abortOnError true
        check 'NewApi', 'InlinedApi'
        fatal 'NewApi', 'InlinedApi'
        enable 'UnusedResources'
        checkReleaseBuilds true
        textReport rootProject.ext.ci
        textOutput 'stdout'
        htmlReport !rootProject.ext.ci
        xmlReport !rootProject.ext.ci
    }
}

// My standard tech stack
dependencies {
    retrolambdaConfig 'com.jakewharton.retrolambda:retrolambda:2.1.0-jake2'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile rootProject.ext.supportAnnotations
    compile rootProject.ext.supportAppCompat
    compile rootProject.ext.supportV4
    compile rootProject.ext.supportDesign
    compile rootProject.ext.supportCustomTabs

    compile rootProject.ext.butterKnife
    compile rootProject.ext.glide
    compile rootProject.ext.glideOkhttp
    compile rootProject.ext.okhttp
    compile rootProject.ext.retrofit
    compile rootProject.ext.retrofitMoshi
    compile rootProject.ext.retrofitGson
    compile rootProject.ext.retrofitRxJava
    compile rootProject.ext.rxAndroid
    compile rootProject.ext.rxBinding
    compile rootProject.ext.rxBindingV4
    compile rootProject.ext.rxJava
    compile rootProject.ext.rxLifecycle
    compile rootProject.ext.timber

    compile rootProject.ext.gson
    compile rootProject.ext.stetho
    compile rootProject.ext.stethoOkHttp

    apt rootProject.ext.barberCompiler
    compile rootProject.ext.barberApi

    apt rootProject.ext.daggerCompiler
    provided rootProject.ext.javax
    compile rootProject.ext.dagger

    provided 'com.jakewharton.auto.value:auto-value-annotations:1.2-update1'
    apt 'com.google.auto.value:auto-value:1.2'
    apt 'com.ryanharter.auto.value:auto-value-moshi:0.2.3-SNAPSHOT'

    compile 'com.bluelinelabs:conductor:1.1.4'
    compile 'com.bluelinelabs:conductor-support:1.1.4'
    compile 'com.bluelinelabs:conductor-rxlifecycle:1.1.4'

    testCompile project(':testing-utils')
    androidTestCompile project(':testing-utils')

    debugCompile rootProject.ext.leakCanary
    releaseCompile rootProject.ext.leakCanaryNoop
}
